###
# Attempts to retrieve all ufokn:IndividualFeatureAtRisk or all ufokn:StandaloneFeatureAtRisk (comment one out)
# Results on UFOKN-Basic dataset
#    IndividualFeatureAtRisk does not retrieve SFaR coded as FaR (but does retrieve IFaRiC coded as FaR)
#    StandaloneFeatureAtRisk does not retrieve SFaR coded as FaR
# Results on UFOKN-2500 / UFOKN-mini datasets
#    IndividualFeatureAtRisk only retrieves IFaRiC coded as FaR
#    StandaloneFeatureAtRisk retrieves nothing

PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX ufokn: <http://schema.ufokn.org/core/v2/>

select ?feature where { 
    ?feature rdf:type ufokn:IndividualFeatureAtRisk.
#    ?feature rdf:type ufokn:StandaloneFeatureAtRisk.
}  



###
# Retrieves all ufokn:CollectionOfFeatures
# Works as expected

PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX ufokn: <http://schema.ufokn.org/core/v2/>

select distinct ?collection where { 
	?collection rdf:type ufokn:CollectionOfFeatures.
    ?collection ufokn:hasMember ?ifaric.
} 



###
# Retrieves all ufokn:CollectionOfFeatures (alternate)
# Works as expected

PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX ufokn: <http://schema.ufokn.org/core/v2/>

select ?feature where {
  ?feature ufokn:hasMember ?unit.
}
group by ?feature
having (count(distinct ?unit) > 1)



###
# Retrieves all ufokn:IndividualFeatureAtRiskInCollection along with address information
# Works as expected, including retrieveing ufokn:FeatureAtRisk that satisfy definition of ufokn:FeatureAtRiskInCollection.

PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX ufokn: <http://schema.ufokn.org/core/v2/>

select ?feature ?st_add where { 
	?feature rdf:type ufokn:IndividualFeatureAtRiskInCollection.
    ?feature ufokn:hasAddress ?address.
    ?address ufokn:hasStreetAddress ?st_address.
    ?st_address ufokn:streetNumber ?st_num.
    ?st_address ufokn:street ?street.
    ?address ufokn:unitNumber ?u_num.
    ?st_address ufokn:city ?city.
    ?st_address ufokn:state ?state.
    ?st_address ufokn:postcode ?zip.
    BIND(concat(str(?st_num), str(" "), str(?street), str(" "), str(?u_num), str(", "), str(?city), str(", "), str(?state), str("  "), str(?zip)) as ?st_add).
} 



###
# Retrieves all ufokn:FeatureAtRisk and attempts to retrieve address information for each
#    Does retrieve all ufokn:FeatureAtRisk when all address lines are commented out.
# The first code to retrieve an address returns nothing, even with the owl:propertyChainAxiom
# The second code only retrieves addresses for ufokn:IndividualFeatureAtRiskInCollection
#    This makes sense as nothing else will have a unit number

PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX ufokn: <http://schema.ufokn.org/core/v2/>

select ?feature ?st_add where { 
	?feature rdf:type ufokn:FeatureAtRisk.
#    ?feature ufokn:hasAddress ?address.
#    ?address ufokn:streetNumber ?st_num.
#    ?address ufokn:street ?street.
#    ?address ufokn:unitNumber ?u_num.
#    ?address ufokn:city ?city.
#    ?address ufokn:state ?state.
#    ?address ufokn:postcode ?zip.
#    BIND(concat(str(?st_num), str(" "), str(?street), str(" "), str(?u_num), str(", "), str(?city), str(", "), str(?state), str("  "), str(?zip)) as ?st_add).
    ?feature ufokn:hasAddress ?address.
    ?address ufokn:hasStreetAddress ?st_address.
    ?st_address ufokn:streetNumber ?st_num.
    ?st_address ufokn:street ?street.
    ?address ufokn:unitNumber ?u_num.
    ?st_address ufokn:city ?city.
    ?st_address ufokn:state ?state.
    ?st_address ufokn:postcode ?zip.
    BIND(concat(str(?st_num), str(" "), str(?street), str(" "), str(?u_num), str(", "), str(?city), str(", "), str(?state), str("  "), str(?zip)) as ?st_add).
}



###
# Retrieves a count of how many ufokn:IndividualFeatureAtRiskInCollection are in each ufokn:CollectionOfFeatures
# Works as expected

PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX ufokn: <http://schema.ufokn.org/core/v2/>

select ?collection (count(?collection) as ?count) where { 
	?collection rdf:type ufokn:CollectionOfFeatures.
    ?collection ufokn:hasMember ?x.
} group by ?collection



###
# Retrieves all distinct key-value pairs

PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX ufokn: <http://schema.ufokn.org/core/v2/>

select distinct ?key ?value where { 
	?x ufokn:key ?key.
    ?x ufokn:value ?value.
} order by ?key ?value



###
# Retrieves all distinct values and the number of times they occur
# Easily adapted to do the same for keys

PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX ufokn: <http://schema.ufokn.org/core/v2/>

select ?val (count(?val) as ?count) where { 
	?X ufokn:value ?val.
} group by ?val



###
# Retrieves all ufokn-c:UtilityAsset entries along with their ufokn-c:UtilityType and ufokn-c:UtilityService
# Works as expected.

PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX ufokn: <http://schema.ufokn.org/core/v2/>
PREFIX ufokn-c: <http://schema.ufokn.org/utility-connection/v2/>

select distinct ?utilityAsset ?serviceType ?utilityService where { 
    ?utilityAsset rdf:type ufokn-c:UtilityAsset.
    ?utilityAsset ufokn-c:ofUtilityType ?serviceType.
    ?utilityAsset ufokn-c:providesUtilityService ?utilityService.
}



###
# Retrieves each ufokn-c:UtilityService, its ufokn-c:UtilityType, and the ufokn-c:UtilityAsset that provides the service
# Works as expected.

PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX ufokn: <http://schema.ufokn.org/core/v2/>
PREFIX ufokn-c: <http://schema.ufokn.org/utility-connection/v2/>

select ?utilityService ?serviceType ?providedByAsset where { 
    ?utilityService rdf:type ufokn-c:UtilityService.
    ?utilityService ufokn-c:ofUtilityType ?serviceType.
    ?utilsutilityServiceerv ufokn-c:providedByUtilityAsset ?providedByAsset.
} 



###
# Retrieves each ufokn-c:UtilityService, its ufokn-c:UtilityType, each ufokn:FeatureOrRegionAtRisk it serves,
#     and the ufokn-c:UtilityAsset that provides the service.
# Works as expected.

PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX ufokn: <http://schema.ufokn.org/core/v2/>
PREFIX ufokn-c: <http://schema.ufokn.org/utility-connection/v2/>

select ?utilityService ?serviceType ?serves ?providedByAsset where { 
    ?utilityService rdf:type ufokn-c:UtilityService.
    ?utilityService ufokn-c:ofUtilityType ?serviceType.
    ?utilityService ufokn-c:providedByUtilityAsset ?providedByAsset.
    ?utilityService ufokn-c:serves ?serves.
} 